#version 460

#extension GL_EXT_ray_tracing : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

//#define DEBUG_PRINTF
#ifdef DEBUG_PRINTF
#extension GL_EXT_debug_printf : enable
bool b = false;
#endif

#include "common_defines.h"
#include "common_structures.h"
#include "random.h"

layout (location = 0) rayPayloadEXT FLightHitPayload LightHitPayload;

layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_TLAS_INDEX) uniform accelerationStructureEXT TLAS;

layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_SPOT_LIGHTS_BUFFER_INDEX) buffer SpotLightsBufferObject
{
    FSpotLight SpotLightsBuffer[];
};

layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_UNIFORM_BUFFER_INDEX) uniform UtilityUniformBufferObject
{
    FUtilitySpotLight UtilitySpotLight;
};

layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_NORMAL_AOV_BUFFER_INDEX) buffer NormalAOVDataBufferObject
{
    vec4 NormalAOVData[];
};

layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_WORLD_SPACE_POSITION_AOV_BUFFER_INDEX) buffer WorldSpacePositionAOVDataBufferObject
{
    vec4 WorldSpacePositionAOVData[];
};


layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_SAMPLED_LIGHT_BUFFER_INDEX) buffer SampledSpotLightBufferObject
{
    vec4 SampledSpotLight[];
};

layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_PIXEL_INDEX_BUFFER) buffer PixelIndexIndexMapBufferObject
{
    uint PixelIndexIndexMap[];
};

layout (set = RAYTRACE_SAMPLE_SPOT_LIGHT_LAYOUT_INDEX, binding = RAYTRACE_SAMPLE_SPOT_LIGHT_RENDER_ITERATION_BUFFER_INDEX) buffer RenderIterationBufferObject
{
    uint RenderIteration;
};

layout (push_constant) uniform PushConstantsBlock
{
    FViewportResolutionPushConstants PushConstants;
};

void main()
{
    uint OriginalPixelIndex = gl_LaunchIDEXT.x;
    uint NewPixelIndex = PixelIndexIndexMap[OriginalPixelIndex];

#ifdef DEBUG_PRINTF
    uvec2 NewPixelCoords = uvec2(NewPixelIndex % PushConstants.Width, NewPixelIndex / PushConstants.Width);

    if (NewPixelCoords.x == PushConstants.Width / 2 && NewPixelCoords.y == PushConstants.Height / 2 && PushConstants.BounceIndex == 0)
    {
        b = true;
        //debugPrintfEXT("CartesianCoordinates1 is %f, %f, %f\n", CartesianCoordinates1.x, CartesianCoordinates1.y, CartesianCoordinates1.z);
        //debugPrintfEXT("RandomFloat is %f\n", f);
    }
#endif

    uint SpotLightsCount = UtilitySpotLight.ActiveLightsCount;

    /// TODO: Use Bounce index also
    FSamplingState SamplingState = FSamplingState(RenderIteration, 0, 0, NewPixelIndex, SAMPLE_TYPE_LIGHT);
    /// Select a random point light
    const uint LightIndex = uint(RandomFloat(SamplingState) * SpotLightsCount);
    FSpotLight SpotLight = SpotLightsBuffer[LightIndex];

    vec4 Normal = NormalAOVData[NewPixelIndex];
    Normal.xyz = Normal.w == 1 ? Normal.xyz : -Normal.xyz;
    vec3 WorldSpacePosition = WorldSpacePositionAOVData[NewPixelIndex].xyz;
    vec3 LightDirection = SpotLight.Position - WorldSpacePosition;
    float LightDistance = length(LightDirection);
    LightDirection = normalize(LightDirection);

    float NDotI = dot(Normal.xyz, LightDirection);
    if(NDotI <= 0)
    {
        SampledSpotLight[NewPixelIndex] = vec4(0);
        return;
    }

    float LightAngle = acos(dot(SpotLight.Direction, -LightDirection));
    /// If point is outside out outer angle, it is also not illuminated
    if (LightAngle > SpotLight.OuterAngle)
    {
        SampledSpotLight[NewPixelIndex] = vec4(0);
        return;
    }

    FRayData RayData;
    RayData.RayFlags = 0;
    RayData.Direction.xyz = LightDirection;
    RayData.Origin.xyz = WorldSpacePosition + Normal.xyz * FLOAT_EPSILON;

    /// Trace the ray
    traceRayEXT(TLAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, RayData.Origin.xyz, 0.000001f, RayData.Direction.xyz, LightDistance, 0);

    /// And if we didn't hit any geometry, then we store light data
    if (LightHitPayload.PayloadFlags == 0)
    {
        float Attenuation = 1.f / LightDistance;
        Attenuation *= Attenuation;

        /// If point between outer and inner angle - interpolate
        if (LightAngle > SpotLight.InnerAngle)
        {
            float Fraction = LightAngle - SpotLight.InnerAngle;
            float Delta = SpotLight.OuterAngle - SpotLight.InnerAngle;
            Fraction = Fraction / Delta;
            /// TODO: pow is wrong... but looks good
            Attenuation *= pow((1. - Fraction), 3);
        }

        SampledSpotLight[NewPixelIndex] = vec4(SpotLight.Color * SpotLight.Intensity * Attenuation, NDotI);
    }
    else
    {
        SampledSpotLight[NewPixelIndex] = vec4(0);
    }
}
