#version 460

#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "common_defines.h"
#include "common_structures.h"
#include "cmj.h"

layout (set = GENERATE_RAYS_LAYOUT_INDEX, binding = CAMERA_RAYS_BUFFER) buffer RaysBufferObject
{
    FRayData RayData[];
};

layout (set = GENERATE_RAYS_LAYOUT_INDEX, binding = CAMERA_POSITION_BUFFER) uniform CameraBufferObject
{
    FDeviceCamera DeviceCamera;
};

layout (set = GENERATE_RAYS_LAYOUT_INDEX, binding = GENERATE_RAYS_PIXEL_INDEX_BUFFER) buffer PixelIndexIndexMapBufferObject
{
    uint PixelIndexIndexMap[];
};

layout (set = GENERATE_RAYS_LAYOUT_INDEX, binding = GENERATE_RAYS_RENDER_ITERATION_BUFFER) buffer RenderIterationBufferObject
{
    uint RenderIteration;
};

layout (set = GENERATE_RAYS_LAYOUT_INDEX, binding = GENERATE_RAYS_THROUGHPUT_BUFFER) buffer ThroughputBufferObject
{
    vec4 ThroughputBuffer[];
};

layout (push_constant) uniform PushConstantsBlock
{
    FPushConstants PushConstants;
};

layout (local_size_x = BASIC_CHUNK_SIZE, local_size_y = 1, local_size_z = 1) in;

void main()
{
    uint PixelIndex = gl_GlobalInvocationID.x;

    if (PixelIndex >= PushConstants.TotalSize)
    {
        return;
    }

    uvec2 PixelCoords = uvec2(PixelIndex % PushConstants.Width, PixelIndex / PushConstants.Width);
    vec2 PixelCoordsF = PixelCoords / vec2(PushConstants.Width, PushConstants.Height);
    /// -= vec2(0.5) so that the center would be (0, 0) and top-left corner (-.5, -.5)
    PixelCoordsF -= vec2(0.5);

    FSamplingState SamplingState = FSamplingState(PixelIndex, 0, RenderIteration);
    const vec2 PixelJitter = (Sample2D(SamplingState) - vec2(0.5)) / vec2(PushConstants.Width, PushConstants.Height);
    PixelCoordsF += PixelJitter;

    vec4 RayOrigin = vec4(DeviceCamera.Origin, 1.f);
    /// We need to adjust height of the sensor cause the framebuffer's aspect ratio might be different from the sensor's one
    const float AdjustedSensorSizeY = DeviceCamera.SensorSizeY * ((DeviceCamera.SensorSizeX / DeviceCamera.SensorSizeY) / (float(PushConstants.Width) / float(PushConstants.Height)));
    vec3 SensorCenter = DeviceCamera.Origin + DeviceCamera.Direction * DeviceCamera.FocalDistance;
    /// "- DeviceCamera.Up * ..." cause we start from the top-left corner
    vec3 PixelOrigin = SensorCenter + DeviceCamera.Right * DeviceCamera.SensorSizeX * PixelCoordsF.x - DeviceCamera.Up * AdjustedSensorSizeY * PixelCoordsF.y;

    vec4 RayDirection = vec4(normalize(PixelOrigin - DeviceCamera.Origin), 1);

    float tMin = 0.001f;
    float tMax = 10000.f;

    RayData[PixelIndex] = FRayData(RayOrigin, RayDirection, 0, tMin, tMax, 1.f);
    ThroughputBuffer[PixelIndex] = vec4(1);
    PixelIndexIndexMap[PixelIndex] = PixelIndex;
}